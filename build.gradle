task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

allprojects {
    repositories {
        jcenter()
    }
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0-rc1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

android {
    buildToolsVersion '21.1.1'
    compileSdkVersion 21
    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 21
        versionCode 1
        versionName "0.0.1-SNAPSHOT"
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.joanzapata.android:android-iconify:1.0.9'
    compile 'com.afollestad:material-dialogs:0.6.2.3'
    compile 'com.melnykov:floatingactionbutton:1.1.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
}

if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
